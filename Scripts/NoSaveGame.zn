//! zinc
library NoSaveGame /* v1.0.0.4
************************************************************************************
*
*    No Save Game
*        by nel
*
*       It prevents to use Save Game function to all players.
*
***********************************************************************************/


       requires

           SimError /* http://www.wc3c.net/showthread.php?t=101260 */


/***********************************************************************************
*
*
*    boolean nsg
*       - Set false, to allow to all players to use Save Game.
*
*    string nsgErrMsg
*       - Error Message
*
*    boolean nsgDisplayErrMsg
*       - Set false, if you don't want to show the Error Message in-game.
*
*
***********************************************************************************/
{
    public {    
        string  nsgErrMsg = "You are not allowed to use Save Game.";
        boolean nsgDisplayErrMsg = true;
        boolean nsg = true;
    }

    private {
        constant dialog DIALOG = DialogCreate();
        constant timer   TIMER = CreateTimer();
    
        module inc { private static method onInit() {
            trigger trgSave = CreateTrigger();
            trigger trgStop = CreateTrigger();
            
            TriggerRegisterGameEvent( trgSave, EVENT_GAME_SAVE );
            TriggerAddCondition( trgSave, Condition( function() -> boolean {
                player pl;
                
                if( nsg ) {
                    pl = GetLocalPlayer();
                    DialogDisplay( pl, DIALOG, true );
                    TimerStart( TIMER, 0, false, null);
                    
                    if( nsgDisplayErrMsg ) { SimError( pl, nsgErrMsg ); }
                    pl = null;
                }
                
                return false;
            }));
            
            TriggerRegisterTimerExpireEvent( trgStop, TIMER );
            TriggerAddCondition( trgStop, Condition( function() -> boolean {
                if( nsg ) { DialogDisplay( GetLocalPlayer(), DIALOG, false ); }
				return false;
            }));
            
            trgSave = null;
            trgStop = null;
        }}
        
        struct Z extends array { module inc; }
    }
}
//! endzinc
